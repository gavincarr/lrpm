#!/usr/bin/perl
#
# lrpm is a simple rpm wrapper that logs successful rpm transactions to /var/log/yum.log
#

use strict;
use File::Basename;
use Time::Piece;
use IO::File;

# Log the following operations
my %log_map = (
  'Installed'   => [ qw(I install) ],
  'Updated' => [ qw(U update) ],
  'Freshened'   => [ qw(F freshen) ],
  'Erased'  => [ qw(E erase) ],
);

my @args = @ARGV;

# Option parsing: assume arguments must end in .rpm, everything else is an option :-)
my (%opts, @rpm, $opts_flag);
while (my $arg = pop @args) {
  if ($arg =~ m/^[^-].*\.rpm$/) {
    die "lrpm can't handle intermixed options and packages - aborting\n" if $opts_flag;
    push @rpm, $arg;
    next;
  }

  $opts_flag ||= 1;
  if ($arg =~ s/^--//) {
    $opts{$arg} = '';
  }
  elsif ($arg =~ s/^-//) {
    $opts{$_} = '' for split //, $arg;
  } 
  else {
    my $opt = pop @args;
    if ($opt =~ s/^--?//) {
      $opts{$opt} = $arg;
    }
    else {
      die "Can't parse '$opt $arg' - expected $opt to be --option, but no hyphens found\n";
    }
  }
}

if ($ENV{LRPM_VERBOSE}) {
  require YAML;
  print "+ opts:\n" . YAML::Dump(\%opts);
  print "+ RPMs: " . join(' ', @rpm) . "\n";
}
    
# Call out to rpm
unless ($ENV{LRPM_NOOP}) {
  my $rv = system('rpm', @ARGV);
  exit $rv >> 8 unless $rv == 0;
}

# RPM call successful - log transactions in %log_map to logfile
my $op;
LOG_MAP:
for my $key (reverse sort keys %log_map) {
  for my $opt (@{$log_map{$key}}) {
    if (exists $opts{$opt}) {
      $op = $key;
      last LOG_MAP;
    }
  }
}
exit unless $op;

my $t = localtime;
my $logfile = $ENV{LRPM_LOG} || '/var/log/yum.log';
my $lf = IO::File->new( $logfile, 'a' )
  or die "Cannot open logfile for writing: $!\n";
for my $rpm (@rpm) {
  $rpm = basename($rpm);
  $rpm =~ s/\.rpm$//;
  $lf->printf("%s %s: %s\n", $t->strftime('%b %d %T'), $op, $rpm);
}

__END__

=head1 NAME

lrpm - logging wrapper for rpm

=head1 USAGE

    # Use exactly as you would use rpm
    lrpm -iv http://mirror.centos.org/centos-5/5.4/updates/x86_64/RPMS/glibc-2.5-42.el5_4.3.x86_64.rpm
    lrpm -Uvh --oldpackage ncurses-5.5-24.20060715.i386.rpm
    lrpm -e bind

=head1 DESCRIPTION

lrpm is a simple logging wrapper for rpm, intended to allow rpm updates to be
captured and logged in the same way as updates via yum.

lrpm just passes all arguments straight through to rpm itself. If rpm returns 
success, and the transaction is not a query, lrpm logs the details to the 
/var/log/yum.log log file, in the standard yum format.

=head1 AUTHOR

Gavin Carr E<lt>gavin@openfusion.com.auE<gt>

=head1 LICENCE

Copyright (C) 2010 Gavin Carr.

This program is free software, licensed under the terms of the GNU General Public License v2.

=cut

